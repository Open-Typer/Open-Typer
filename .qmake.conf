FULLVER=5.1.1
VERSION=$$section(FULLVER, -, 0, 0)

win32 {
    GIT_LOCATION = $$system(where git)
}
!win32 {
    GIT_LOCATION = $$system(which git)
}
DEFINES += BUILD_VERSION=\\\"$$FULLVER\\\"
isEmpty(GIT_LOCATION) | !exists(.git) {
    exists(revision) {
        DEFINES += BUILD_REVISION=\\\"$$cat(revision)\\\"
    }
} else {
    DEFINES += BUILD_REVISION=\\\"$$system(git rev-parse --short HEAD)\\\"
}

CONFIG += c++17
CONFIG += qtquickcompiler
DESTDIR = $$PWD

INCLUDEPATH += \
    $$PWD/src \
    $$PWD/src/framework

!wasm {
    LIBS += -lssl -lcrypto
}

# The following define makes your compiler emit warnings if you use
# any Qt feature that has been marked deprecated (the exact warnings
# depend on your compiler). Please consult the documentation of the
# deprecated API in order to know how to port your code away from it.
DEFINES += QT_DEPRECATED_WARNINGS

# You can also make your code fail to compile if it uses deprecated APIs.
# In order to do so, uncomment the following line.
# You can also select to disable deprecated APIs only up to a certain version of Qt.
#DEFINES += QT_DISABLE_DEPRECATED_BEFORE=0x060000    # disables all the APIs deprecated before Qt 6.0.0
